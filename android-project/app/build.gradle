def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY')
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
	apply plugin: 'com.android.application'
} else {
	apply plugin: 'com.android.library'
}
task prepareKotlinBuildScriptModel {

}
android {
	ndkVersion '25.2.9519653'
	compileSdk 33
	defaultConfig {
		if (buildAsApplication) {
			applicationId "org.devxmods.wkdmod"
		}
		minSdkVersion 18
		targetSdkVersion 33
		versionCode 30
		versionName project.file('../../VERSION').text.trim()
		externalNativeBuild {
			cmake {
				arguments "-DANDROID_STL=c++_static"
				abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
			}
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	namespace 'org.diasurgical.devilutionx'
	applicationVariants.all { variant ->
		tasks["merge${variant.name.capitalize()}Assets"]
			.dependsOn("externalNativeBuild${variant.name.capitalize()}")
	}
	if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
		sourceSets.main {
			jniLibs.srcDir 'libs'
		}
		externalNativeBuild {
			cmake {
				path '../../CMakeLists.txt'
				version "3.22.1+"
				buildStagingDirectory System.env.DevilutionX_Android_StagingDirectory
			}
		}

	}

	if (buildAsLibrary) {
		libraryVariants.all { variant ->
			variant.outputs.each { output ->
				def outputFile = output.outputFile
				if (outputFile != null && outputFile.name.endsWith(".aar")) {
					def fileName = "org.devxmods.wkdmod.aar"
					output.outputFile = new File(outputFile.parent, fileName)
				}
			}
		}
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'com.android.support.constraint:constraint-layout:2.0.4'
}
